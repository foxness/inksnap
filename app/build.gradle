apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "me.nocturnl.inksnap"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 3
        versionName "1.0.2"
        resConfigs "en"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
//            shrinkResources true TODO
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
        usable {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.config
            renderscriptDebuggable false
            renderscriptOptimLevel 3
            minifyEnabled true
            applicationIdSuffix '.usable'
            versionNameSuffix '-usable'
            zipAlignEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin/'
        debug.java.srcDirs += 'src/debug/kotlin/'
        usable.java.srcDirs += 'src/usable/kotlin/'
        release.java.srcDirs += 'src/release/kotlin/'
    }
    productFlavors {
    }
}

dependencies {
    def support_version = "28.0.0" // todo: use androidx instead of support library
    def room_version = "1.1.1"
    def khttp_version = "0.1.0"
    def jodatime_version = "2.10"
    def kotlinx_coroutines_version = "0.24.0"
    def jodaconvert_version = "2.1.1"
    
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    
    implementation "android.arch.persistence.room:runtime:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"

    implementation "khttp:khttp:$khttp_version" // todo: get rid of this

    implementation "joda-time:joda-time:$jodatime_version"
    implementation group: 'org.joda', name: 'joda-convert', version: jodaconvert_version, classifier: 'classic'
}
repositories {
    mavenCentral()
}
